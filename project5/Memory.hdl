// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 *
 *  0x6000 - 110000000000000 - 24576
 *  0x5FFF - 101111111111111 - 24575
 *  0x4000 - 100000000000000 - 16384
 *  
 *
 *  RAM | 0 - 16383 | 000000000000000 - 011111111111111
 *  Screen | 16384 - 24575 | 100000000000000 - 101111111111111
 *  Keyboard | 24576 | 110000000000000
 *
 *    out = a if sel = 00
 *       b if sel = 01
 *       c if sel = 10
 *       d if sel = 11
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    DMux4Way(in=load, sel=address[13..14], a=dmRAMONE, b=dmRAMTWO, c=dmSCREEN, d=outKEY);
    Or(a=dmRAMONE, b=dmRAMTWO, out=dmRAM);
	RAM16K(in=in, load=dmRAM, address=address[0..13], out=outDATA);
    Screen(in=in, load=dmSCREEN, address=address[0..12], out=outSCREEN);
    Keyboard(out=outKEYBOARD);
    Mux4Way16(a=outDATA, b=outDATA, c=outSCREEN, d=outKEYBOARD, sel=address[13..14], out=out);
}